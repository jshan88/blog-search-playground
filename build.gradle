plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id "org.sonarqube" version "3.5.0.2730"
}

group 'com.jshan'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'com.jshan'
    version '1.0-SNAPSHOT'
    java {
        sourceCompatibility = '17'
    }

    repositories {
        mavenCentral()
    }
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
    ext {
        set('springCloudVersion', "2022.0.3")
    }
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    dependencies {
        // M1 netty 오류
        if (System.getProperty('os.name').toLowerCase().contains('mac')) {
            implementation 'io.netty:netty-resolver-dns-native-macos:4.1.94.Final:osx-aarch_64'
        }
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }
    tasks.named("bootJar") {
        archiveClassifier = 'boot'
    }
    tasks.named("jar") {
        archiveClassifier = ''
    }
}

bootJar {
    enabled = false
}
jar {
    enabled = true
}